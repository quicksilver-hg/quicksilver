# Copyright (c) 2023-present The Bitcoin Core developers
# Copyright (c) 2026 The Quicksilver developers
# Distributed under the MIT software license, see the accompanying
# file COPYING or https://opensource.org/license/mit/.

include_guard(GLOBAL)

function(setup_split_debug_script)
  if(CMAKE_HOST_SYSTEM_NAME STREQUAL "Linux")
    set(OBJCOPY ${CMAKE_OBJCOPY})
    set(STRIP ${CMAKE_STRIP})
    configure_file(
      contrib/devtools/split-debug.sh.in split-debug.sh
      FILE_PERMISSIONS OWNER_READ OWNER_EXECUTE
                       GROUP_READ GROUP_EXECUTE
                       WORLD_READ
      @ONLY
    )
  endif()
endfunction()

function(add_maintenance_targets)
  if(NOT PYTHON_COMMAND)
    return()
  endif()

  foreach(target IN ITEMS quicksilverd quicksilver-qt quicksilver-cli quicksilver-agent quicksilver-tx quicksilver-util quicksilver-vault test_quicksilver bench_quicksilver)
    if(TARGET ${target})
      list(APPEND executables $<TARGET_FILE:${target}>)
    endif()
  endforeach()

  add_custom_target(check-symbols
    COMMAND ${CMAKE_COMMAND} -E echo "Running symbol and dynamic library checks..."
    COMMAND ${PYTHON_COMMAND} ${PROJECT_SOURCE_DIR}/contrib/devtools/symbol-check.py ${executables}
    VERBATIM
  )

  add_custom_target(check-security
    COMMAND ${CMAKE_COMMAND} -E echo "Checking binary security..."
    COMMAND ${PYTHON_COMMAND} ${PROJECT_SOURCE_DIR}/contrib/devtools/security-check.py ${executables}
    VERBATIM
  )
endfunction()

function(add_windows_deploy_target)
  if(MINGW AND TARGET quicksilver-qt AND TARGET quicksilverd AND TARGET quicksilver-cli AND TARGET quicksilver-agent AND TARGET quicksilver-tx AND TARGET quicksilver-vault AND TARGET quicksilver-util)
    # TODO: Consider replacing this code with the CPack NSIS Generator.
    #       See https://cmake.org/cmake/help/latest/cpack_gen/nsis.html
    include(GenerateSetupNsi)
    generate_setup_nsi()
    add_custom_command(
      OUTPUT ${PROJECT_BINARY_DIR}/quicksilver-win64-setup.exe
      COMMAND ${CMAKE_COMMAND} -E make_directory ${PROJECT_BINARY_DIR}/release
      COMMAND ${CMAKE_STRIP} $<TARGET_FILE:quicksilver-qt> -o ${PROJECT_BINARY_DIR}/release/$<TARGET_FILE_NAME:quicksilver-qt>
      COMMAND ${CMAKE_STRIP} $<TARGET_FILE:quicksilverd> -o ${PROJECT_BINARY_DIR}/release/$<TARGET_FILE_NAME:quicksilverd>
      COMMAND ${CMAKE_STRIP} $<TARGET_FILE:quicksilver-cli> -o ${PROJECT_BINARY_DIR}/release/$<TARGET_FILE_NAME:quicksilver-cli>
      COMMAND ${CMAKE_STRIP} $<TARGET_FILE:quicksilver-agent> -o ${PROJECT_BINARY_DIR}/release/$<TARGET_FILE_NAME:quicksilver-agent>
      COMMAND ${CMAKE_STRIP} $<TARGET_FILE:quicksilver-tx> -o ${PROJECT_BINARY_DIR}/release/$<TARGET_FILE_NAME:quicksilver-tx>
      COMMAND ${CMAKE_STRIP} $<TARGET_FILE:quicksilver-vault> -o ${PROJECT_BINARY_DIR}/release/$<TARGET_FILE_NAME:quicksilver-vault>
      COMMAND ${CMAKE_STRIP} $<TARGET_FILE:quicksilver-util> -o ${PROJECT_BINARY_DIR}/release/$<TARGET_FILE_NAME:quicksilver-util>
      COMMAND makensis -V2 ${PROJECT_BINARY_DIR}/quicksilver-win64-setup.nsi
      VERBATIM
    )
    add_custom_target(deploy DEPENDS ${PROJECT_BINARY_DIR}/quicksilver-win64-setup.exe)
  endif()
endfunction()

function(add_macos_deploy_target)
  if(CMAKE_SYSTEM_NAME STREQUAL "Darwin" AND TARGET quicksilver-qt)
    set(macos_app "Quicksilver-Qt.app")
    # Populate Contents subdirectory.
    configure_file(${PROJECT_SOURCE_DIR}/share/qt/Info.plist.in ${macos_app}/Contents/Info.plist NO_SOURCE_PERMISSIONS)
    file(CONFIGURE OUTPUT ${macos_app}/Contents/PkgInfo CONTENT "APPL????")
    # Populate Contents/Resources subdirectory.
    file(CONFIGURE OUTPUT ${macos_app}/Contents/Resources/empty.lproj CONTENT "")
    configure_file(${PROJECT_SOURCE_DIR}/src/qt/res/icons/quicksilver.icns ${macos_app}/Contents/Resources/quicksilver.icns NO_SOURCE_PERMISSIONS COPYONLY)
    file(CONFIGURE OUTPUT ${macos_app}/Contents/Resources/Base.lproj/InfoPlist.strings
      CONTENT "{ CFBundleDisplayName = \"@CLIENT_NAME@\"; CFBundleName = \"@CLIENT_NAME@\"; }"
    )

    add_custom_command(
      OUTPUT ${PROJECT_BINARY_DIR}/${macos_app}/Contents/MacOS/Quicksilver-Qt
      COMMAND ${CMAKE_COMMAND} --install ${PROJECT_BINARY_DIR} --config $<CONFIG> --component quicksilver-qt --prefix ${macos_app}/Contents/MacOS --strip
      COMMAND ${CMAKE_COMMAND} -E rename ${macos_app}/Contents/MacOS/bin/$<TARGET_FILE_NAME:quicksilver-qt> ${macos_app}/Contents/MacOS/Quicksilver-Qt
      COMMAND ${CMAKE_COMMAND} -E rm -rf ${macos_app}/Contents/MacOS/bin
      COMMAND ${CMAKE_COMMAND} -E rm -rf ${macos_app}/Contents/MacOS/share
      VERBATIM
    )

    add_custom_target(deploydir
      DEPENDS ${PROJECT_BINARY_DIR}/${macos_app}/Contents/MacOS/Quicksilver-Qt
    )
    add_custom_target(deploy
      DEPENDS deploydir
    )
    add_dependencies(deploydir quicksilver-qt)
  endif()
endfunction()
